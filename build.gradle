plugins {
    id 'java'
    id 'application'
}

group 'com.8thlight'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.10

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        java {
            srcDir 'test-integration'
        }
        resources {
            srcDir 'test-integration'
        }
    }
}

dependencies {
    compile('net.sourceforge.argparse4j:argparse4j:0.8.1')
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '5.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile('org.hamcrest:hamcrest-junit:2.0.0.0')
    testCompile('com.github.grantwest.eventually:hamcrest-eventually-matchers:0.0.1')
    testCompile('org.junit.jupiter:junit-jupiter-api:5.3.0')
    testCompile('org.junit.jupiter:junit-jupiter-params:5.3.0')
    testCompile('org.quicktheories:quicktheories:0.25')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.0')
    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'
}

test {
    useJUnitPlatform {
        excludeTags 'acceptance'
    }

}

mainClassName = 'com.eighthlight.fabrial.App'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

run {
    systemProperties([
        "rootLevel": System.getenv("LOGLEVEL") ?: "TRACE"
    ])
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task testAcceptance(dependsOn: 'jar', type: Test) {
    description = 'Runs the acceptance tests against assembled jar.'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath



    useJUnitPlatform {
        includeTags 'acceptance'
        includeEngines 'junit-jupiter'
    }
}
testAcceptance.dependsOn tasks.fatJar
